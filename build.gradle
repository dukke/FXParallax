plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = '1.8'

group = 'com.pixelduke'
version = '1.0.0'
def versionDescription = 'FXParallax First release'


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
    manifest {
        attributes( 'Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    options.addStringOption('javafx', '-quiet')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        //userOrg = 'dukke'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dukke/FXParallax.git'
        publications = ['mavenJava']
        websiteUrl = 'https://www.pixelduke.com/fxparallax'
        issueTrackerUrl = 'https://github.com/dukke/FXParallax/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = versionDescription // Version description
            vcsTag = project.version
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

